--
-- VHDL ARCHITECTURE ECE411.ALU.UNTITLED
--
-- CREATED:
--          BY - HERSTAD.STDT (EESN26.EWS.UIUC.EDU)
--          AT - 13:36:07 02/19/03
--
-- GENERATED BY MENTOR GRAPHICS' HDL DESIGNER(TM) 2001.5 (BUILD 170)
--
-- HDS INTERFACE_START
LIBRARY IEEE;
USE IEEE.STD_LOGIC_1164.ALL;
USE IEEE.NUMERIC_STD.ALL;
LIBRARY ECE411;
USE ECE411.LC3B_TYPES.ALL;

ENTITY ALU IS
PORT( 
	A     : IN     LC3B_WORD;
	ALUOP : IN     LC3B_ALUOP;
	B     : IN     LC3B_WORD;
	F     : OUT    LC3B_WORD
);
-- DECLARATIONS
END ALU ;

-- HDS INTERFACE_END
ARCHITECTURE UNTITLED OF ALU IS
BEGIN
	ALU : PROCESS(A,B,ALUOP)
	VARIABLE STATE : LC3B_WORD;
	VARIABLE SHAMT : INTEGER RANGE 0 TO 15;
	BEGIN
		--THE SHIFT AMOUNT IS THE 4 LEAST SIGNIFICANT BITS OF B
		SHAMT := TO_INTEGER(UNSIGNED(B(3 DOWNTO 0)));

		CASE ALUOP IS
		WHEN ALU_AND =>
			STATE := A AND B;
		WHEN ALU_ADD =>
			STATE := STD_LOGIC_VECTOR(SIGNED(A) + SIGNED(B));
		WHEN ALU_NOT =>
			STATE := NOT A;
		WHEN ALU_PASS =>
			STATE := A;
		WHEN ALU_SLL =>
			STATE := STD_LOGIC_VECTOR("SLL"(UNSIGNED(A), SHAMT));
		WHEN ALU_SRL =>
			STATE := STD_LOGIC_VECTOR("SRL"(UNSIGNED(A), SHAMT));
		WHEN ALU_SRA =>
			IF (SHAMT = 0) THEN
				STATE := A;
			ELSE
				STATE(15 - SHAMT DOWNTO 0) := A(15 DOWNTO SHAMT);
				STATE(15 DOWNTO (15 - SHAMT + 1)) := (OTHERS => A(15));
			END IF;
		WHEN OTHERS =>
			STATE := (OTHERS => 'X');
			ASSERT FALSE REPORT "INVALID ALU OPERATION (ALUOP)." SEVERITY WARNING;
		END CASE;
		F <= STATE AFTER DELAY_ALU;
	END PROCESS ALU;
END UNTITLED;